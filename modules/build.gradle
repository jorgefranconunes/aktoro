/**************************************************************************
 *
 * Copyright (c) 2017 Aktoro project contributors.
 *
 **************************************************************************/

import org.gradle.testing.jacoco.tasks.JacocoReport

import static com.varmateo.gradle.Utils.onlyIfTaskSelected


ext.aktoroVersion =  "0.0.1-SNAPSHOT"

/**
 * Versions of dependencies to be used by all sub-projects.
 */
ext.assertjCoreVersion = "3.6.1"
ext.junitVersion = "4.11"
ext.mockitoCoreVersion = "2.2.27"

description = "Aktoro"


/**
 *
 */
allprojects {
    repositories {
        mavenCentral()
    }
}


/**
 * Configuration common to all sub-projects
 */
subprojects {
    apply plugin : 'java'

    version = aktoroVersion
    group = "com.varmateo.aktoro"

    repositories {
        mavenCentral()
    }

    project.sourceCompatibility = "1.8"

    apply from: "${rootDir}/gradle/checkstyleConfig.gradle"
    apply from: "${rootDir}/gradle/findbugsConfig.gradle"
    apply from: "${rootDir}/gradle/jacocoConfig.gradle"
    //apply from: "${rootDir}/gradle/javadocConfig.gradle"
    apply from: "${rootDir}/gradle/pmdConfig.gradle"
    apply from: "${rootDir}/gradle/testConfig.gradle"
}


/**
 *
 */
task clean(type: Delete) {

    delete buildDir

    dependsOn subprojects.collect { it.tasks.getByName("clean") }
}


/**
 *
 */
task check {
    dependsOn subprojects.collect { it.tasks.getByName("check") }
}


/**
 *
 */
task build {
    dependsOn subprojects.collect { it.tasks.getByName("build") }
    dependsOn check
}


/**
 * Jacoco aggregate report
 */
onlyIfTaskSelected("jacocoReport") {

    apply plugin: 'jacoco'

    task jacocoReport(type: JacocoReport) {

        def projectRootDir = project.rootDir.absolutePath
        executionData fileTree(projectRootDir).include("**/build/jacoco/*.exec")

        subprojects.each {
            sourceSets it.sourceSets.main
        }

        reports {
            xml.enabled true
            xml.destination file("${buildDir}/reports/jacoco/report.xml")
            html.enabled true
            csv.enabled false
        }

        dependsOn subprojects.collect { it.tasks.withType(Test) }
    }

    check.dependsOn jacocoReport
}


/**
 *
 */
//apply from: "${rootDir}/gradle/aggregatedReportsConfig.gradle"
