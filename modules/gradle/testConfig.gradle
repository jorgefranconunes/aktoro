/**************************************************************************
 *
 * Copyright (c) 2017 Aktoro project contributors.
 *
 **************************************************************************/

/**
 * Unit tests configuration.
 *
 * Example of running one single test from the command line:
 *
 *    gw test --tests \*.ActorRefImplTest
 */
test {
    include "**/*Test.class"

    testLogging {
        events "standard_out", "standard_error"
        exceptionFormat "full"
        showExceptions true
        showCauses true
        showStackTraces true
    }

    afterSuite { descriptor, result ->
        if ( (descriptor.parent!=null) && (descriptor.className!=null) ) {
            def duration = result.endTime - result.startTime
            def total = result.testCount
            def successes = result.successfulTestCount
            def failures = result.failedTestCount
            def skiped = result.skippedTestCount
            println "${result.resultType} ${failures}/${successes}/${total} ${duration} ms : ${descriptor.name}"
        }
    }

    // We do not generate the HTML report for each sub-project because
    // the aggregatedTestReport will also generate HTML.
    reports.html.enabled = false
}
